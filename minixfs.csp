datatype CallType = C | R | X
datatype B = GetB | PutB
datatype S = Alloc | Free
datatype I = GetI | PutI
datatype F = Read | Write
datatype D = Lookup | Link
datatype SC = Fopen | Fclose | Fread | Fwrite

channel dev_read, dev_write
channel b : B.CallType
channel s : S.CallType
channel i : I.CallType
channel f : F.CallType
channel d : D.CallType
channel sc : SC.CallType
channel client : SC.Int
channel recurse

CACHE_SIZE=5
OPEN_FILES=5
WAIT_READ=2
WAIT_WRITE=2

-- -- n = number of outstanding reads
-- -- m = number of 'open' blocks
-- -- o = number of outstanding writes
-- BlockCache(n,m,o) = (m > 0 and o < WAIT_WRITE) & b.PutB.X -> BlockCache(n,m-1,o+1)
--                  [] ((n + m) < CACHE_SIZE) & b.GetB.C -> BlockCache(n+1,m,o)
--                  [] (n > 0) & dev_read -> b.GetB.R -> BlockCache(n-1,m+1,o)
--                  [] (o > 0) & dev_write -> BlockCache(n,m,o-1)
--
-- BlockCache0 = BlockCache(0,0,0)
--
-- Superblock = s.Alloc.C -> b.GetB.C -> b.GetB.R -> b.PutB.X -> s.Alloc.R -> Superblock
--           [] s.Free.C -> b.GetB.C -> b.GetB.R ->  b.PutB.X -> s.Free.R -> Superblock
--
-- InodeCache(n,m,o) = (m > 0 and o < WAIT_WRITE) & i.PutI.X -> InodeCache(n,m-1,o+1)
--                  [] ((n + m) < CACHE_SIZE) & i.GetI.C -> InodeCache(n+1,m,o)
--                  [] (n > 0) & b.GetB.C -> b.GetB.R -> i.GetI.R -> b.PutB.X -> InodeCache(n-1,m+1,o)
--                  [] (o > 0) & b.GetB.C -> b.GetB.R -> b.PutB.X -> InodeCache(n,m,o-1)
--
-- InodeCache0 = InodeCache(0,0,0)

-- n = number of outstanding reads
-- m = number of 'open' blocks
-- o = number of outstanding writes
BlockCache(n,m) = (m > 0) & b.PutB.X -> dev_write -> BlockCache(n,m-1)
                 [] ((n + m) < CACHE_SIZE) & b.GetB.C -> BlockCache(n+1,m)
                 [] (n > 0) & dev_read -> b.GetB.R -> BlockCache(n-1,m+1)

BlockCache0 = BlockCache(0,0)

Superblock = s.Alloc.C -> b.GetB.C -> b.GetB.R -> b.PutB.X -> s.Alloc.R -> Superblock
          [] s.Free.C -> b.GetB.C -> b.GetB.R ->  b.PutB.X -> s.Free.R -> Superblock

InodeCache(n,m) = (m > 0) & i.PutI.X -> b.GetB.C -> b.GetB.R -> b.PutB.X -> InodeCache(n,m-1)
                 [] ((n + m) < CACHE_SIZE) & i.GetI.C -> InodeCache(n+1,m)
                 [] (n > 0) & b.GetB.C -> b.GetB.R -> i.GetI.R -> b.PutB.X -> InodeCache(n-1,m+1)

InodeCache0 = InodeCache(0,0)

Finode = f.Read.C -> FinodeReading
      [] f.Write.C -> FinodeWriting

FinodeReading = b.GetB.C -> b.GetB.R -> b.PutB.X -> recurse -> FinodeReading
             [] b.GetB.C -> b.GetB.R -> b.PutB.X -> f.Read.R -> Finode

FinodeWriting = b.GetB.C -> b.GetB.R -> b.PutB.X -> recurse -> FinodeWriting
             [] b.GetB.C -> b.GetB.R -> b.PutB.X -> s.Alloc.C -> s.Alloc.R -> s.Free.C -> s.Free.R -> recurse -> FinodeWriting
             [] b.GetB.C -> b.GetB.R -> b.PutB.X -> f.Write.R -> Finode

Dinode = d.Lookup.C -> b.GetB.C -> b.GetB.R -> b.PutB.X -> i.GetI.C -> i.GetI.R -> i.PutI.X -> d.Lookup.R -> Dinode
      [] d.Link.C -> b.GetB.C -> b.GetB.R -> b.PutB.X -> s.Alloc.C -> s.Alloc.R -> s.Free.C -> s.Free.R -> b.GetB.C -> b.GetB.R -> b.PutB.X -> d.Link.R -> Dinode

Syscalls = sc.Fopen.C -> d.Lookup.C -> d.Lookup.R -> s.Alloc.C -> s.Alloc.R -> d.Link.C -> d.Link.R -> sc.Fopen.R -> Syscalls
        [] sc.Fclose.C -> sc.Fclose.R -> Syscalls
        [] sc.Fread.C -> f.Read.C -> f.Read.R -> sc.Fread.R -> Syscalls
        [] sc.Fwrite.C -> f.Write.C -> f.Write.R -> sc.Fwrite.R -> Syscalls

Alpha = Dinode ||| Finode
Beta = Syscalls [| {|d.Lookup, d.Link, f.Read, f.Write|} |] Alpha
Gamma = Superblock [| {|s.Alloc, s.Free|} |] Beta
Delta = InodeCache0 [| {|i.GetI, i.PutI|} |] Gamma
FileSystem = BlockCache0 [| {|b.GetB, b.PutB|} |] Delta
FileSystemH = FileSystem \ InternalEvents

InternalEvents = {|b, s, i, f, d, dev_read, dev_write|}
SyscallEvents = {|sc|}

Client(id,n) = (n < 3) & sc.Fopen.C -> sc.Fopen.R -> client.Fopen.id -> Client(id,n+1)
            [] (n > 0) & sc.Fclose.C -> sc.Fclose.R -> client.Fclose.id -> Client(id,n-1)
            [] (n > 0) & sc.Fread.C -> sc.Fread.R -> client.Fread.id -> Client(id,n)
            [] (n > 0) & sc.Fwrite.C -> sc.Fwrite.R -> client.Fwrite.id -> Client(id,n)

ClientSpec(id,n) = ((n < 3) & client.Fopen.id -> ClientSpec(id,n+1)
               |~| (n > 0) & client.Fclose.id -> ClientSpec(id,n-1)
               |~| (n > 0) & client.Fread.id -> ClientSpec(id,n)
               |~| (n > 0) & client.Fwrite.id -> ClientSpec(id,n))
               |~| recurse -> ClientSpec(id,n)

NClients(n) = (||| x : {0..n-1} @ Client(n,0))
NClientSpec(n) = ||| x : {0..n-1} @ ClientSpec(n,0)

FS1Client = FileSystemH [| SyscallEvents |] NClients(1)
FS2Client = FileSystemH [| SyscallEvents |] NClients(2)
FS3Client = FileSystemH [| SyscallEvents |] NClients(3)
FS4Client = FileSystemH [| SyscallEvents |] NClients(4)
FS5Client = FileSystemH [| SyscallEvents |] NClients(5)

assert FileSystem [| SyscallEvents |] NClients(1) [T= STOP
assert NClientSpec(1) [T= FS1Client \ SyscallEvents
assert NClientSpec(2) [T= FS2Client \ SyscallEvents
assert NClientSpec(3) [T= FS3Client \ SyscallEvents
assert NClientSpec(4) [T= FS4Client \ SyscallEvents
assert NClientSpec(5) [T= FS5Client \ SyscallEvents

assert NClientSpec(1) [F= FS1Client \ SyscallEvents
assert NClientSpec(2) [F= FS2Client \ SyscallEvents
assert NClientSpec(3) [F= FS3Client \ SyscallEvents
assert NClientSpec(4) [F= FS4Client \ SyscallEvents
assert NClientSpec(5) [F= FS5Client \ SyscallEvents

assert NClientSpec(1) [FD= FS1Client \ SyscallEvents
assert NClientSpec(2) [FD= FS2Client \ SyscallEvents
assert NClientSpec(3) [FD= FS3Client \ SyscallEvents
assert NClientSpec(4) [FD= FS4Client \ SyscallEvents
assert NClientSpec(5) [FD= FS5Client \ SyscallEvents
